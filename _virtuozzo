#compdef prlctl
#
# Virtuozzo 7 prlctl zsh completion
# Based on https://docs.virtuozzo.com/master/index.html

local -a _1st_arguments
_1st_arguments=(
  'backup:Backs up the specified virtual environment.'
  'backup-list:Lists the existing backups.'
  'backup-delete:Delete the backup for specified virtual environment.'
  'restore:Restore the specified virtual environment.'
  'capture:Take a screenshot of a current virtual environment console in PNG format.'
  'screenshot:Take a screenshot of a current virtual environment console in PNG format.'
  'clone:Make a copy of a virtual environment'
  'console:Attach to the Containers console.'
  'create:Create a virtual environment'
  'delete:Delete a virtual environment'
  'destroy:Delete a virtual environment'
  'installtools:Connect  the  Virtuozzo  Guest  Tools image to the VE using its CD/DVD-ROM device,'
  'enter:Log in to the virtual environment. For virtual machines,'
  'exec:Execute the command command in the virtual environment.'
  'list:List the virtual environments currently existing on the Virtuozzo server.'
  'migrate:Migrates the specified virtual environment to a different server.'
  'pause:Pause the specified virtual environment.'
  'register:Register a virtual environment.'
  'reset:Reset the specified virtual environment.'
  'resume:Resume the specified virtual environment.'
  'restart:Restart the specified virtual environment.'
  'start:Start the specified virtual environment.'
  'status:Display the status of the specified virtual environment.'
  'change-sid:change the Windows security identifier (SID) of a VM'
  'stop:Stop the specified virtual environment.'
  'snapshot:This command is used to create VE snapshot.'
  'snapshot-delete:Deletes snapshot with the specified snapid.'
  'snapshot-list:This  command is used to list the virtual environments snapshots tree.'
  'snapshot-switch:Used to revert to selected snapshot.'
  'suspend:Suspend the specified virtual environment.'
  'unregister:Unregister the specified virtual environment.'
  'reset-uptime:Resets the specified virtual environment uptime counter (counter start date/time also will be reset with this action)'
  'mount:Mounts the specified virtual environment.'
  'umount:Unmounts the specified virtual environment.'
  'move:Moves the directory with files of the specified virtual environment to a new location on the same server.'
  'problem-report:Generate a problem report.'
  'statistics:Print performance statistics for running virtual machines and containers on the server.'
  'set:This command is used to set and configure various VE parameters.'
)

_guests() {
  while read -r guest
  do
    _wanted 'guest' expl 'guest' compadd $guest
  done <<< "$( prlctl list --all --no-header -o name 2>/dev/null )"
}

_templates() {
  while read -r template
  do
    _wanted 'template' expl 'template' compadd $template
  done <<< "$( prlctl list -t --no-header -o name 2>/dev/null )"
}

_distribution_list()
{
  while read -r distribution
  do

    _wanted 'distribution' expl 'distribution' compadd $distribution
  done <<< "$( prlctl create whocares -d list | grep -Ev 'The following values are allowed|Experimental support' | tr -cs 'A-Za-z0-9._-' '[\n*]'  )"
}

_ostype_list()
{
  while read -r ostype
  do

    _wanted 'ostype' expl 'ostype' compadd $ostype
  done <<< "$( prlctl create whocares -o list | grep -Ev 'The following values are allowed|Experimental support' | tr -cs 'A-Za-z0-9._-' '[\n*]'  )"
}

_arguments '*:: :->subcmds' && return 0

if (( CURRENT == 1 )); then
  _describe -t commands "prctl commands" _1st_arguments -V1
  return
fi

case "$words[1]" in
  list)
    _arguments \
      {-a,--all}'[List all, running, stopped, suspended, and paused virtual machines]' \
      {-t,--template}'[List the available virtual machine templates]' \
      '--no-header[Do not display column headers]' \
      {-o,--output}'[Specify field(s) to output]:type:(uuid envid status name dist owner system-flags description numproc ip ip_configured hostname netif mac features location iolimit netdev type ha_enable ha_prio)' \
      {-s,--sort}'[Sort output in ascending order by specified field]:type:(uuid envid status name dist owner system-flags description numproc ip ip_configured hostname netif mac features location iolimit netdev type ha_enable ha_prio)' \
      {-i,--info}'[Display detailed information about a virtual machine]' \
      '-L[List fields which can be used for both the output (-o, --output) and sort order (-s, --sort) options.]' \
      '--vmtype[Display only virtual environments of the specified type.:type:(ct vm all)]' \
      {-j,--json}'[Produce output in the JSON format.]' \
      {-f,--full}'[Display additional information about virtual environments..]'
  ;;

#  backup )
#    _arguments \
#      :guest:_guests
#  ;;

#  backup-list )
#    _arguments \
#      :guest:_guests
#  ;;

#  backup-delete )
#    _arguments \
#      :guest:_guests
#  ;;

#  restore )
#    _arguments \
#      :guest:_guests
#  ;;

  capture )
    _arguments \
      :guest:_guests \
      '--file[Path where to save the screenshot to]:dir:_directories'
  ;;

  screenshot )
    _arguments \
      :guest:_guests \
      '--file[Path where to save the screenshot to]:dir:_directories'
  ;;

  clone )
    _arguments \
      :guest:_guests \
      {-n,--name}'[name of the clone]' \
      '--template[create a template of a virtual environment.]' \
      '--dst[specify the path to the directory where the directory with files of the cloned virtual environment will be stored.]' \
      '--changesid[option to assign the clone a new Windows security identifier (SID)]' \
      '--detach-external-hdd[specify whether to keep or remove hard disks located outside of the original virtual environment.]'
  ;;

  console | destroy | installtools | enter | pause | reset | resume | restart | start | status | change-sid | suspend | unregister | reset-uptime | umount)
    _arguments \
      :guest:_guests
  ;;

  create )
    _arguments \
      ': :->guestname' \
      '--ostemplate[template to be used]:template:_templates' \
      '--vmtype[Display only virtual environments of the specified type.]:type:(ct vm)' \
      {-o,--ostype}'[optimize it for use with the operating system (OS) family specified]:ostype:_ostype_list' \
      {-d,--distribution}'[optimize it for use with the distribution specified]:distribution:_distribution_list' \
      '--uuid[manually specify the UUID to use.]' \
      '--changesid[assign the new Windows-based virtual machine a new Windows security identifier (SID).]' \
      '--dst[path to the directory where the newly created virtual environment will be stored]:dir:_directories' \
      '--no-hdd[create virtual environment without hard disk drives.]'
  ;;

  delete )
    _arguments \
      :guest:_guests \
      '--force'
  ;;

  exec )
    _arguments \
      :guest:_guests \
      '--whithout-shell[run command directly without bash or cmd shell.]'
  ;;

#  migrate )
#    _arguments \
#      :guest:_guests
#  ;;

  register )
    _arguments \
      '*:dir:_directories' \
      '--preserve-uuid[virtual environment ID will not changed]' \
      '--uuid=-[the provided UUID will be used for virtual environment ID,]' \
      '--regenerate-src-uuid[virtual environment source ID will be regenerated (SMBIOS product id will be changed as well)]' \
      '--force[all validation checks will be skipped]'
  ;;

  stop )
    _arguments \
      :guest:_guests \
      '--kill' \
      '--noforce'
  ;;

  snapshot )
    _arguments \
      :guest:_guests \
      {-n,--name}'[name of the snapshot]'
  ;;

#  snapshot-delete )
#    _arguments \
#      :guest:_guests
#  ;;

#  snapshot-list )
#    _arguments \
#      :guest:_guests
#  ;;

#  snapshot-switch )
#    _arguments \
#      :guest:_guests
#  ;;

  mount )
    _arguments \
      :guest:_guests \
      '-o[read-only(ro) or read-write(rw, default)]:type:(ro rw)'
  ;;

  move )
    _arguments \
      :guest:_guests \
      '--dst[destination]:dir:_directories'
  ;;

  problem-report )
    _arguments \
      :guest:_guests \
      {-d,--dump,-s,--send}'[dump output to stdout (-s) or send to Virtuozzo Development Team (-s)]' \
      '--proxy[proxy to use]' \
      '--no-proxy:[dont use proxy]'
  ;;

  statistics )
    _arguments \
      :guest:_guests \
      '--loop:[Print statistics every second until the program is terminated.]'
  ;;

  set )
    _arguments \
      :guest:_guests \
        '--cpus[number of CPU cores per CPU socket]' \
        '--cpu-sockets[number of CPU sockets]' \
        '--memsize[amount of memory that the virtual environment can consume]' \
        '--videosize[amount of memory for the virtual environment graphic card]' \
        '--3d-accelerate[]:type:(off highest dx9)' \
        '--vertical-sync[]:type:(on off)' \
        '--cpuunits[CPU weight for the virtual environment]' \
        '--cpulimit[CPU limit, in percent or megahertz (MHz)]' \
        '--cpumask[CPUs on the physical server to use for executing the virtual environment process.]' \
        '--nodemask[NUMA node on the physical server to use for executing the virtual environment process.]' \
        '--cpu-hotplug[]:type:(on off)' \
        '--ioprio[I/O  priority. Higher is better. Default is priority of 4.]' \
        '--iolimit[I/O limit]' \
        '--iopslimit[Input/Output Operations Per Second limit]' \
        '--mem-hotplug[]:type:(on off)' \
        '--memguarantee[auto or value]' \
        '--description[Set the VE description.]' \
        '--autostart[virtual environment start-up option]:type:(on off auto)' \
        '--autostart-delay[Delay some seconds at virtual environment autostart]' \
        '--autostop[mode to set the virtual environment on the Virtuozzo Service shutdown]:type:(stop suspend)' \
        '--autocompact[Turns on/off automatic virtual disk image compact]:type:(on off)' \
        '--tools-autoupdate[automatic updating of Virtuozzo Guest Tools in the guest operating system]:type:(on off)' \
        '--on-crash[what to do with the virtual environment if it crashes: pause or restart.]:type:(pause restart restart:no-report)' \
        '--vnc-mode[Enables/disables access to the virtual environment via the VNC protocol]:type:(on manual off)' \
        '--vnc-port[VNC port]' \
        '--vnc-passwd[VNC password]' \
        '--vnc-vncnopasswd[Do not require a password for VNC connections.]' \
        '--vnc-address[VNC address]' \
        '--name[Change the VE name.]' \
        '--rename-ext-disks[]' \
        '--userpasswd[password for the specified user in the virtual environment]' \
        '--host-admin[a host OS administrators name if an administrators password is required to change the password for the specified user in the virtual environment.]' \
        '--netfilter[Restrict access to iptable modules inside the Container]:type:(disabled stateless stateful full)' \
        '--swappages[limits the amount of swap space that can be allocated to processes running in a Container]' \
        '--swap[limits the amount of swap space that can be allocated to processes running in a Container.]' \
        '--quotaugidlimit[maximum  number of user/group IDs in a Container for which disk quota is calculated]' \
        '--device-add[Adding virtual devices to VE]:type:(hdd cdrom net fdd serial usb)' \
        '--device-del[Removing virtual devices from VE]' \
        '--destroy-image[the virtual HDD image is removed from the server]' \
        '--destroy-image-force[the virtual HDD image is removed from the server and all snapshots]' \
        '--detach-only[if the device is a virtual hard disk drive, the disk image is preserved]' \
        '--device-set[Used to configure various parameters of the specified virtual device.]' \
        '--device-connect[Used to connect the specified device to a running VE.]' \
        '--device-disconnect[Disconnect the specified device.]' \
        '--device-bootorder[Used to specify the order of boot devices for a virtual environment]' \
        '--image[image name]:file:_files' \
        '--offline-management[Enable/disable the offline management feature]:type:(on off)' \
        '--hostname[Sets the hostname for the virtual environment]' \
        '--nameserver[Sets the nameserver for the virtual environment]' \
        '--searchdomain[Sets the searchdomain for the virtual environment]' \
        '--rate[Specifies the bandwidth guarantee of the virtual environment for the specified network class.]' \
        '--ratebound[If set to "yes", the bandwidth guarantee is also the limit for the virtual environment]:type:(yes no)' \
        '--applyconfig[Apply the resource parameter values from the specified VE configuration file to the virtual environment]' \
        '--nested-virt[Disable or enable nested virtualization.]:type:(on off)' \
        '--efi-boot[EFI boot]:type:(on off)' \
        '--select-boot-device[Enable or disable the selection of a boot device at the virtual environment startup]:type:(on off)' \
        '--external-boot-device[Set an external device from which to boot the virtual environment]' \
        '--ha-enable[Adds the virtual environment to (yes) or removes it (no) from the High Availability Cluster]:type:(yes no)' \
        '--ha-prio[Sets the virtual environment priority in the High Availability Cluster. Higher is better]' \
        '--template[Convert the virtual environment to template and vice versa]:type:(yes no)' \
        '--backup-add[]' \
        '--backup-del[]'
  ;;
esac
